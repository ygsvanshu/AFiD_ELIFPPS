!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!                                                         ! 
!    FILE: ReadFlowField.F90                              !
!    CONTAINS: subroutine ReadFlowField                   !
!                                                         ! 
!    PURPOSE: Initialization routine. Reads in a flow     !
!     snapshot generated by a previous simulation, and    !
!     interpolates to the current grid if necessary       !
!                                                         !
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

subroutine ReadFlowField

    use mpih
    use decomp_2d
    use local_arrays
    use param

    implicit none

    character*200     :: filename,dsetname
    integer           :: nxo,nyo,nzo
    integer           :: st1,en1,st2,en2,st3,en3
    logical           :: exists
    
    filename = trim('continua.h5')

    inquire(file=filename,exist=exists)
    if (.not.exists) then 
        write(*,*) 'Continuation file not found'
        call MpiAbort
    end if
  
    if (ismaster) then
        dsetname = trim('/nxm')
        call HdfSerialReadIntScalar(filename,dsetname,nxo)
        dsetname = trim('/nym')
        call HdfSerialReadIntScalar(filename,dsetname,nyo)
        dsetname = trim('/nzm')
        call HdfSerialReadIntScalar(filename,dsetname,nzo)
        dsetname = trim('/time')
        call HdfSerialReadRealScalar(filename,dsetname,time)
    endif
        
    call MpiBarrier
    call MpiBcastInt(nxo)
    call MpiBcastInt(nyo)
    call MpiBcastInt(nzo)
    call MpiBcastReal(time)
    
    !EP   Check whether grid specifications have been updated
    if((nxo.ne.nxm).or.(nyo.ne.nym).or.(nzo.ne.nzm)) then

        if(ismaster) write(*,*) "!! Interpolation not supported at this time !!"
        call MpiAbort

    else

        !--------------------------------- X-VELOCITY ---------------------------------!

        dsetname = trim('/vx')

        st1 = 1
        en1 = nx

        st2 = xstart(2)
        en2 = xend(2)

        if (xstart(2).eq.1) st2 = 0
        if (xend(2).eq.nym) en2 = ny

        st3 = xstart(3)
        en3 = xend(3)

        if (xstart(3).eq.1) st3 = 0
        if (xend(3).eq.nzm) en3 = nz
        
        call HdfParallelReadReal3D(filename,dsetname,vx(st1:en1,st2:en2,st3:en3),st1,en1,st2,en2,st3,en3,1,nx,0,ny,0,nz,mpi_comm)

        !--------------------------------- Y-VELOCITY ---------------------------------!

        dsetname = trim('/vy')

        st1 = 0
        en1 = nx

        st2 = xstart(2)
        en2 = xend(2)

        if (xend(2).eq.nym) en2 = ny

        st3 = xstart(3)
        en3 = xend(3)

        if (xstart(3).eq.1) st3 = 0
        if (xend(3).eq.nzm) en3 = nz

        call HdfParallelReadReal3D(filename,dsetname,vy(st1:en1,st2:en2,st3:en3),st1,en1,st2,en2,st3,en3,0,nx,1,ny,0,nz,mpi_comm)

        !--------------------------------- Z-VELOCITY ---------------------------------!

        dsetname = trim('/vz')

        st1 = 0
        en1 = nx

        st2 = xstart(2)
        en2 = xend(2)

        if (xstart(2).eq.1) st2 = 0
        if (xend(2).eq.nym) en2 = ny

        st3 = xstart(3)
        en3 = xend(3)

        if (xend(3).eq.nzm) en3 = nz

        call HdfParallelReadReal3D(filename,dsetname,vz(st1:en1,st2:en2,st3:en3),st1,en1,st2,en2,st3,en3,0,nx,0,ny,1,nz,mpi_comm)

        !---------------------------------- PRESSURE ----------------------------------!

        dsetname = trim('/pr')

        st1 = 0
        en1 = nx

        st2 = xstart(2)
        en2 = xend(2)

        if (xstart(2).eq.1) st2 = 0
        if (xend(2).eq.nym) en2 = ny

        st3 = xstart(3)
        en3 = xend(3)

        if (xstart(3).eq.1) st3 = 0
        if (xend(3).eq.nzm) en3 = nz

        call HdfParallelReadReal3D(filename,dsetname,pr(st1:en1,st2:en2,st3:en3),st1,en1,st2,en2,st3,en3,0,nx,0,ny,0,nz,mpi_comm)
        
    endif
  
    return
  
end subroutine ReadFlowField